if(isAloneRun.toBoolean()){
    apply plugin: 'com.android.application'
}else{
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion COMPILE_SDK_VERSION.toInteger()



    defaultConfig {
        // 独立运行时添加 applicationId ，集成调试时不需要
        if(isAloneRun.toBoolean()){
            applicationId "com.xzb.login"
        }

        minSdkVersion MIN_SDK_VERSION.toInteger()
        targetSdkVersion TARGET_SDK_VERSION.toInteger()
        if(isAloneRun.toBoolean()){
            versionCode LOGIN_VERSION_CODE.toInteger()
            versionName LOGIN_VERSION_NAME
        }else{
            versionCode VERSION_CODE.toInteger()
            versionName VERSION_NAME
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    //通过给子模块设置不同的资源前缀，可以避免重复命名
    resourcePrefix "loginlib_"

    sourceSets{
        main{
            //独立运行时与集成调试时使用不同的 AndroidManifest.xml 文件
            if(isAloneRun.toBoolean()){
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }else{
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestCompile('com.android.support:support-annotations:26.1.0') { //使用arouter时，support-annotations问题
        force = true
    }
    //依赖baseCore工程lib
    implementation project(':basecore')
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2' //多组件工程时，这里annotationProcessor依然需要
}
